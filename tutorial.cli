#
# Copyright (C) 2001-2014 Olof Hagsand
#
#  This file is part of CLIgen.
#
#  CLIgen is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2, or (at your option)
#  any later version.
#
#  CLIgen is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with CLIgen; see the file COPYING.  

  prompt="hello> ";            # Assignment of prompt
  comment="#";                 # Same comment as in syntax
  name="tutorial";             # Name of syntax (used when referencing)
  hello("Greet the world"){    # 'hello' command with help text
    world, hello("Hello World!"); # 'world' command with callback
  }
  # Nested command using callback arguments to differentiate
  ip("The IP protocol"){       
    tcp("The TCP protocol") <port:int32>("Port number"), cb("tcp");
    udp("The UDP protocol") <addr:ipaddr>("IPv4 address"), cb("udp");
  }
  # Example of commands on several levels
  aa bb,cb();{              
    ff,cb();
  }
  # Example of complex variable matching
  values (<int32> | <string> | <int64> | aa), cb();
  # Complex syntax and callback argument handling
  aa bb (ca("help ca") <int32>|cb("help cb")) [dd|ee], letters();
  # Expand example
  interface {
    <ifname:string interface()>("interface name"),cb(); 
  }
  # Example of hidden command
  secret,hide,secret("message");        
  # Example of changing prompt
  change prompt <new:string>("New prompt"),setprompt(); 
  
  # Example of sub-tree reference (here recursion).
  recurse @tutorial;

   # Quit CLI
  quit("quit the CLI"),quit(); 
